apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "io.github.hidroh.tldroid"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 9
        versionName "1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat 'full'
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        htmlReport false
        xmlReport false
        textReport true
        warningsAsErrors true
        abortOnError true
        informational 'InvalidPackage', // https://github.com/square/okio/issues/58
                'UnusedResources' // https://youtrack.jetbrains.com/issue/KT-7729
        explainIssues false
        absolutePaths false
    }

    // espresso-web
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    compile 'com.android.support:design:24.2.0',
            'com.android.support.constraint:constraint-layout:1.0.0-alpha7', // TODO http://b.android.com/212128
            'com.github.rjeschke:txtmark:0.13',
            'com.squareup.moshi:moshi:1.2.0',
            'com.squareup.okio:okio:1.8.0'
    kapt "com.android.databinding:compiler:$androidToolsVersion"
    testCompile 'junit:junit:4.12',
            'org.mockito:mockito-core:1.10.19',
            'org.powermock:powermock-module-junit4:1.6.2',
            'org.powermock:powermock-api-mockito:1.6.2',
            'org.assertj:assertj-core:1.7.1', // test against android.jar which uses JDK6
            'com.squareup.okhttp3:mockwebserver:3.3.1'
    androidTestCompile 'org.assertj:assertj-core:2.4.1',
            'com.android.support:support-annotations:24.2.0',
            'com.android.support.test:runner:0.5',
            'com.android.support.test:rules:0.5',
            'com.android.support.test.espresso:espresso-core:2.2.2',
            'com.android.support.test.espresso:espresso-idling-resource:2.2.2',
            'com.android.support.test.espresso:espresso-intents:2.2.2',
            'com.android.support.test.espresso:espresso-web:2.2.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

kapt {
    generateStubs = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['connectedAndroidTest', 'testDebugUnitTest']) {
    sourceDirectories = files android.sourceSets.main.java.srcDirs
    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            exclude: ['**/R.class', '**/R$*.class', '**/databinding/**']
    )
    executionData = fileTree(
            dir: "${buildDir}",
            include: ['**/*.exec', '**/*.ec']
    )
    reports {
        html.destination "${buildDir}/reports/jacoco/html"
    }
    doLast {
        println "jacoco report generated to file://${reports.html.destination}/index.html"
    }
}
repositories {
    mavenCentral()
}
